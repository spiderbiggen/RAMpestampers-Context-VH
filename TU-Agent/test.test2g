use TUAgent as mas.
use agentKnowledge as knowledge.
use tygron as module.
use tygron as actionspec.

timeout = 30.

% This is the main test file, it tests the percept and some general indicator goals and beliefs.
test tygron with
	pre {
		% We should have at least an indicatorGoal for this module to run.
		goal(indicatorGoal(_, _)),
		% Tests for some important believes which are needed for this module to function.
		bel(functions(FunctionsList), not(empty(FunctionsList))),
		bel(stakeholders(StakeholdersList), not(empty(StakeholdersList))),
		bel(buildings(BuildingsList), not(empty(BuildingsList))),
		bel(indicators(IndicatorsList), not(empty(IndicatorsList)))
		}
	in {
		% Failures in Percept Processing.
		% We only percept this only once because of the empty list believe.
		percept(buildings(X)), bel(buildings([])) 
			leadsto bel(buildings(X)).
		% Tests for percepts that are only send on change, according to template P-on-change.
		% Test stakeholders percept.
		percept(stakeholders(X)) 
			leadsto bel(stakeholders(X)).
		percept(stakeholders(X)), bel(stakeholders(Y), not(X=Y)) 
			leadsto not(bel(stakeholders(Y))).
		% Test settings percept.
		percept(settings(X)) 
			leadsto bel(settings(X)).
		percept(settings(X)), bel(settings(Y), not(X=Y)) 
			leadsto not(bel(settings(Y))).
		% Test functions percept.
		percept(functions(X)) 
			leadsto bel(functions(X)).
		percept(functions(X)), bel(functions(Y), not(X=Y)) 
			leadsto not(bel(functions(Y))).
		% Test indicators percept.
		percept(indicators(X)) 
			leadsto bel(indicators(X)).
		percept(indicators(X)), bel(indicators(Y), not(X=Y)) 
			leadsto not(bel(indicators(Y))).
		% Test lands percept.
		percept(lands(X)) 
			leadsto bel(lands(X)).
		percept(lands(X)), bel(lands(Y), not(X=Y)) 
			leadsto not(bel(lands(Y))).
		% Test zones percept.
		percept(zones(X)) 
			leadsto bel(zones(X)).
		percept(zones(X)), bel(zones(Y), not(X=Y)) 
			leadsto not(bel(zones(Y))).
		% Test requests percept.
		percept(requests(X)) 
			leadsto bel(requests(X)).
		percept(requests(X)), bel(requests(Y), not(X=Y)) 
			leadsto not(bel(requests(Y))).
		% Test actions percept.
		percept(actions(X)) 
			leadsto bel(actions(X)).
		percept(actions(X)), bel(actions(Y), not(X=Y)) 
			leadsto not(bel(actions(Y))).
		% Test upgradeTypes percept.
		percept(upgradeTypes(X)) 
			leadsto bel(upgradeTypes(X)).
		percept(upgradeTypes(X)), bel(upgradeTypes(Y), not(X=Y)) 
			leadsto not(bel(upgradeTypes(Y))).
		
		% Failures in Goal Management.
		% The goal should be reconsidered and dropped because of the believe, according to template G2.
		bel(relevant_areas(0, MPList), not(empty(MPList))) 
			leadsto not(goal(createLandToBuild)).		
		% The believe is the reason for not having the goal here, according to template G3.
		never goal(indicatorGoal(Name, Target)), bel(indicator(_, Name, Current, _), Target > 0, Current >= Target). 
		never goal(indicatorGoal(Name, Target)), bel(indicator(_, Name, Current, _), Target =< 0, Current =< Target).	
	}
 	post {true}

	tygronagent {
		do tygron.
	}
	